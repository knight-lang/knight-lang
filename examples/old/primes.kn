; = primes_upto BLOCK
	# Make a list of all numbers from 2 to max.
	# We make use of the fact that the list->int conversion
	# is the length of the list.
	; = all @
	; WHILE > max all
		: = all + all (, LENGTH all)

	; = i 2
	# For each number between 2 to max:
	; WHILE < i max
		; = j * 2 i
		# For every multiple of `i` (but not `i` itself):
		; WHILE < j max
			# Replace that duplicate with `0`, ie a falsey value.
			; = all (SET all j 1 (,0))
			: = j + j i
		# Finally, increment `i`.
		: = i + 1 i

	# Create an initially empty list of all primes.
	; = primes @

	# Remove the first two elements, ie `0` and `1`, as they arent primes.
	; = all ]]all
	# While there's still values left in `all`
	; WHLIE all
		# If the first value is nonzero,
		; & (= t [all)
			# add that to the list of primes
			: = primes + primes ,t
		# Then delete the first element from `all`
		: = all ]all
	: primes

; = max + 0 PROMPT
; OUTPUT ++ "the primes from 2-" max " are:"
: OUTPUT ^ (CALL primes_upto) " "
